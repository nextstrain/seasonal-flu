"""
This is the main ingest Snakefile that orchestrates the full ingest workflow
and defines its default outputs.
"""
# The workflow filepaths are written relative to this Snakefile's base directory
workdir: workflow.current_basedir

# Use default configuration values. Override with Snakemake's --configfile/--config options.
configfile: "defaults/config.yaml"

VALID_DATASETS = list(
    key
    for key, value in config['filtering'].items()
    if isinstance(value, dict)
)

wildcard_constraints:
    # Expected datasets should match the standardized outputs of the `filtering` block
    # (example datasets are "h3n2", "avian-flu")
    # in scripts/standardized-lineage
    dataset = r'|'.join(VALID_DATASETS),
    segment = r'pb2|pb1|pa|ha|np|na|mp|ns',
    # Constrain GISAID pair names to "gisaid_cache" or YYYY-MM-DD-N
    gisaid_pair = r'gisaid_cache|\d{4}-\d{2}-\d{2}(-\d+)?'


rule all:
    input:
        metadata = expand("results/{dataset}/metadata.tsv", dataset=VALID_DATASETS),
        sequences = expand("results/{dataset}/{segment}.fasta", dataset=VALID_DATASETS, segment=config["segments"]),


include: "rules/prepare_ndjson.smk"
include: "rules/curate.smk"


if "custom_rules" in config:
    for rule_file in config["custom_rules"]:

        include: rule_file
