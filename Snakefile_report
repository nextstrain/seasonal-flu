include: "Snakefile_WHO"

resolutions = ['2y']
lineages = ['h3n2', 'h1n1pdm', 'vic', 'yam']
segments = ['ha', 'na']
centers = ['cdc', 'vidrl', 'crick', 'niid', 'who']
assays = ['hi', 'fra']
passages = ['cell', 'egg']

def mutations_to_plot(v):
    mutations = {('h3n2', 'ha'):["HA1:128A", "HA1:131K", "HA1:135K", "HA1:135N",
                                 "HA1:142G", "HA1:212T", "HA1:214T", "HA1:91N"],
                 ('h3n2', 'na'):["NA:329S", "NA:386S", "NA:126L"],
                 ('h1n1pdm', 'ha'):["HA1:164T","HA1:120A","HA1:129D", "HA1:183P", "HA1:235D", "HA1:233I"],
                 ('h1n1pdm', 'na'):["NA:77R", "NA:93H", "NA:74L", "NA:416N"],
                 ('vic', 'ha'):["HA1:163-", "HA1:165N", "HA1:175V", "HA1:209N"],
                 ('vic', 'na'):["NA:371Q","NA:375K"],
                 ('yam', 'ha'):["HA1:229N","HA1:232N"],
                 ('yam', 'na'):["NA:342K", "NA:395S", "NA:65H"],
                  }
    return mutations[(v.lineage, v.segment)]

def clades_to_plot(v):
    clades = {('h3n2', 'ha'):["A1b/131K", "A1b/135K", "A1b/135N", "A1", "A2/re",'A3', '3c3.A'],
              ('h1n1pdm', 'ha'):["6b1.A", "6b1.A1", "6b1.A2","6b1.A3","6b1.A4","6b1.A5"],
              ('vic', 'ha'):["V1A", "V1A.1", "V1A/165N"],
              ('yam', 'ha'):["172Q", "3"],
              }
    return clades[(v.lineage, v.segment)]

regions_to_graph = ['north_america', 'europe', 'china', 'oceania', 'japan_korea', 'south_america']


rule figures:
    input:
        expand("figures/mutation_frequencies_{lineage}_{segment}_{resolution}.pdf", segment=segments, lineage=lineages, resolution=resolutions),
        expand("figures/clade-frequencies_{lineage}_ha_{resolution}.pdf", lineage=lineages, resolution=resolutions),
        expand("figures/age-distribution_{lineage}_{resolution}.pdf", lineage=lineages, resolution=resolutions),
        expand("figures/titer_matrix_{center}_{lineage}_ha_{resolution}_{passage}_{assay}.pdf",
                lineage=lineages, resolution=resolutions, center=centers, passage=passages, assay=assays)



rule mutation_frequency_graphs:
    input:
        mutations = expand("results/mutation_frequencies_{region}_{{lineage}}_{{segment}}_{{resolution}}.json",
                    region=regions_to_graph),
    params:
        mutations = mutations_to_plot,
        regions = regions_to_graph+['global'],
    output:
        mutations = "figures/mutation_frequencies_{lineage}_{segment}_{resolution}.pdf",
        total_counts = "figures/total-sample-count_{lineage}_{segment}_{resolution}.pdf",
    shell:
        """
        python scripts/graph_frequencies.py --mutation-frequencies {input.mutations} \
                                            --mutations {params.mutations} \
                                            --regions {params.regions} \
                                            --output-mutations {output.mutations} \
                                            --output-total-counts {output.total_counts} \
        """

rule clade_frequency_graphs:
    input:
        tree = "results/tree-frequencies_cdc_{lineage}_{segment}_{resolution}_cell_hi.json",
        clades = "results/clades_cdc_{lineage}_{segment}_{resolution}_cell_hi.json"
    params:
        regions = regions_to_graph+['global'],
        clades = clades_to_plot,
    output:
        tree_counts = "figures/tree-sample-count_{lineage}_{segment}_{resolution}.pdf",
        clades = "figures/clade-frequencies_{lineage}_{segment}_{resolution}.pdf"
    shell:
        """
        python scripts/graph_frequencies.py --tree-frequencies {input.tree} \
                                            --clade-annotation {input.clades} \
                                            --clades {params.clades} \
                                            --regions {params.regions} \
                                            --output-clades {output.clades} \
                                            --output-tree-counts {output.tree_counts}
        """


rule mutation_statistics:
    input:
        mutations = rules.complete_mutation_frequencies_by_region.output.mut_freq,
        node_data = rules.translate.output.node_data
    params:
        offset = 4,
        n_out=20
    output:
        rising = "results/rising_mutations_{region}_{lineage}_{segment}_{resolution}.txt",
        recurring_mut = "results/recurring_mutations_{region}_{lineage}_{segment}_{resolution}.txt",
        recurring_pos = "results/recurring_positions_{region}_{lineage}_{segment}_{resolution}.txt"
    run:
        from scripts.mutation_statistics import rising_mutations, recurring_mutations
        rising_mutations(input.mutations, offset=params.offset, fname=output.rising, n_out=params.n_out)

        recurring_mutations(input.node_data, fname_by_position=output.recurring_pos, fname_by_mutation=output.recurring_mut, n_out=params.n_out)


rule age_distributions:
    input:
        metadata = "results/metadata_{lineage}_ha.tsv",
        exclude = files.outliers
    params:
        resolution="{resolution}"
    output:
        "figures/age-distribution_{lineage}_{resolution}.pdf"
    shell:
        """
        python scripts/age_distributions.py  --metadata {input.metadata} \
                                             --resolution {params.resolution} \
                                             --exclude {input.exclude} \
                                             --output {output}
        """

rule titer_matrix:
    input:
        titers = "auspice-who/flu_{center}_{lineage}_{segment}_{resolution}_{passage}_{assay}_titers.json",
        clades = "results/clades_{center}_{lineage}_{segment}_{resolution}_{passage}_{assay}.json"
    output:
        "figures/titer_matrix_{center}_{lineage}_{segment}_{resolution}_{passage}_{assay}.pdf"
    shell:
        '''
        python3 scripts/plot_titer_matrices.py --titers {input.titers} \
                                               --clades {input.clades} \
                                               --output {output}
        '''

